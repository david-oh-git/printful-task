
import io.davidosemwota.template.buildsource.utils.SystemOSUtilsKt
import io.davidosemwota.template.buildsource.BuildTaskGroups

// Task to copy files from git-hooks to .get/hooks/
task copyGitHooks(type: Copy) {
    description 'Copies the git hooks from ../git-hooks to the .git folder.'
    group BuildTaskGroups.gitHooks
    from("${rootDir}/scripts/git-hooks/") {
        include '**/*.sh'
        rename '(.*).sh', '$1'
    }
    into "${rootDir}/.git/hooks"
//    onlyIf { isLinuxOrMacOs() }
}

task installGitHooks(type: Exec) {
    description 'Installs the pre-commit git hooks from ../git-hooks.'
    group BuildTaskGroups.gitHooks
    workingDir rootDir
    commandLine 'chmod'
    args '-R', '+x', '.git/hooks/'
    onlyIf { SystemOSUtilsKt.isLinuxOrMacOs() }
    dependsOn copyGitHooks
    doLast {
        logger.info('Git hook installed successfully.')
    }

}

task deleteGitHooks(type: Delete) {
    description 'Delete the pre-commit git hooks.'
    group BuildTaskGroups.gitHooks
    delete(".git/hooks")
}



